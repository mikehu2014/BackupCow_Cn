unit UFormLocalBackupPath;

interface

uses
  Windows, Messages, SysUtils, Classes, Graphics, Controls, Forms, Dialogs,
  VirtualTrees, StdCtrls,
  ImgList, ComCtrls, ExtCtrls, SyncObjs, UIconUtil, RzPanel, RzDlgBtn, RzTabs,
  Spin, pngimage, UFrameFilter, UFileBaseInfo;

type

  // This data record contains all necessary information about a particular file system object.
  // This can either be a folder (virtual or real) or an image file.
  PShellObjectData = ^TShellObjectData;
  TShellObjectData = record
    FullPath, Display: WideString;
    IsFolder : Boolean;
    FileSize : Int64;
    FileTime : TDateTime;
  end;


  TfrmSelectLocalBackupPath = class(TForm)
    Panel1: TPanel;
    Panel2: TPanel;
    btnCancel: TButton;
    btnOK: TButton;
    PcMain: TRzPageControl;
    tsSelectFile: TRzTabSheet;
    TsGenernal: TRzTabSheet;
    TsFilter: TRzTabSheet;
    plDestinationSettings: TPanel;
    gbDestinationSettings: TGroupBox;
    plBackupSttings: TPanel;
    gbBackupSettins: TGroupBox;
    Image2: TImage;
    cbbSyncTimeType: TComboBox;
    chkIsBackupNow: TCheckBox;
    chkIsAutoSync: TCheckBox;
    seSyncTimeValue: TSpinEdit;
    chkIsDisable: TCheckBox;
    vstSelectPath: TVirtualStringTree;
    LvDestination: TListView;
    FrameFilter: TFrameFilterPage;
    ilPcMain16: TImageList;
    Label1: TLabel;
    btnNext: TButton;
    chkIsKeepDeleted: TCheckBox;
    seKeepEditionCount: TSpinEdit;
    lbKeepEditionCount: TLabel;
    procedure FormCreate(Sender: TObject);
    procedure vdtBackupFolderHeaderClick(Sender: TVTHeader; Column: TColumnIndex; Button: TMouseButton; Shift: TShiftState; X,
      Y: Integer);
    procedure btnOKClick(Sender: TObject);
    procedure btnCancelClick(Sender: TObject);
    procedure vstSelectPathGetText(Sender: TBaseVirtualTree;
      Node: PVirtualNode; Column: TColumnIndex; TextType: TVSTTextType;
      var CellText: WideString);
    procedure vstSelectPathFreeNode(Sender: TBaseVirtualTree;
      Node: PVirtualNode);
    procedure vstSelectPathGetImageIndex(Sender: TBaseVirtualTree;
      Node: PVirtualNode; Kind: TVTImageKind; Column: TColumnIndex;
      var Ghosted: Boolean; var ImageIndex: Integer);
    procedure vstSelectPathInitChildren(Sender: TBaseVirtualTree;
      Node: PVirtualNode; var ChildCount: Cardinal);
    procedure vstSelectPathInitNode(Sender: TBaseVirtualTree; ParentNode,
      Node: PVirtualNode; var InitialStates: TVirtualNodeInitStates);
    procedure vstSelectPathChecked(Sender: TBaseVirtualTree;
      Node: PVirtualNode);
    procedure FormShow(Sender: TObject);
    procedure FrameIncludebtnSelectFileClick(Sender: TObject);
    procedure FrameExcludebtnSelectFileClick(Sender: TObject);
    procedure btnNextClick(Sender: TObject);
    procedure PcMainPageChange(Sender: TObject);
    procedure chkIsKeepDeletedClick(Sender: TObject);
  private
    FDriveStrings: string;
    function GetDriveString(Index: Integer): string;
    procedure AddDeskTopPath;
  private
    procedure ReadDefaultConfig;
    procedure FindSelectPath( Node : PVirtualNode; SelectPathList : TStringList ); // Find Path
    function RemoveNewChecked( Node : PVirtualNode ): Boolean;overload;
    procedure SetUnChecked( Node : PVirtualNode );   // 清空 Checked
  public
    procedure AddBackupPath( FullPath : string );  // Add Path
    procedure RemoveBackupPath( FullPath : string ); // Remove Path
  public
    function getNewSelectList : TStringList;  // get SelectList
    function getLocalBackupConfigInfo : TLocalBackupConfigInfo;
    procedure RemoveNewChecked;overload;  // Remove Select
  end;

    // 读取 默认配置
  TReadBackupDefaultConfig = class
  public
    procedure Update;
  private
    procedure SetGenernal;
    procedure SetDestination;
    procedure SetFilter;
  end;

    // 提取 窗口中的配置
  TGetLocalBackupConfig = class
  private
    LocalBackupConfigInfo : TLocalBackupConfigInfo;
  public
    function get : TLocalBackupConfigInfo;
  private
    procedure FindGenernal;
    procedure FindDestination;
    procedure FindFilter;
  end;

const
  VstSelectLocalBackupPath_FileName = 0;
  VstSelectLocalBackupPath_FileSize = 1;
  VstSelectLocalBackupPath_FileTime = 2;

var
  frmSelectLocalBackupPath: TfrmSelectLocalBackupPath;
  DeskTopPath : string;

//----------------------------------------------------------------------------------------------------------------------

implementation

uses
  FileCtrl, ShellAPI, Mask, ShlObj, ActiveX, UMyUtil, ULocalBackupControl, USettingInfo,
  ULocalBackupFace;

{$R *.DFM}

procedure TfrmSelectLocalBackupPath.FormCreate(Sender: TObject);
var
  SFI: TSHFileInfo;
  i, Count, DriverCount: Integer;
  DriveMap, Mask: Cardinal;
  RootNode : PVirtualNode;
  RootData : PShellObjectData;
  DriverPath : string;
begin
  vstSelectPath.NodeDataSize := SizeOf(TShellObjectData);
  vstSelectPath.Images := MyIcon.getSysIcon;

  // Fill root level of image tree. Determine which drives are mapped.
  DriverCount := 0;
  DriveMap := GetLogicalDrives;
  Mask := 1;
  for i := 0 to 25 do
  begin
    if (DriveMap and Mask) <> 0 then
      Inc(DriverCount);
    Mask := Mask shl 1;
  end;

  // Determine drive strings which are used in the initialization process.
  Count := GetLogicalDriveStrings(0, nil);
  SetLength(FDriveStrings, Count);
  GetLogicalDriveStrings(Count, PChar(FDriveStrings));

    // 初始化 磁盘路径
  for i := 0 to DriverCount - 1 do
  begin
    DriverPath := GetDriveString(i);
    if not MyHardDisk.getDriverExist( DriverPath ) then
      Continue;

    RootNode := vstSelectPath.AddChild( vstSelectPath.RootNode );
    RootData := vstSelectPath.GetNodeData( RootNode );
    RootData.FullPath := DriverPath;
    RootData.Display := DriverPath;
    RootData.FileTime := MyFileInfo.getFileLastWriteTime( DriverPath );
    RootData.FileSize := MyHardDisk.getHardDiskAllSize( DriverPath );
    RootData.IsFolder := False;
  end;

    // 添加 桌面路径
  AddDeskTopPath;

    // 初始化
  FrameFilter.IniFrame;
  LvDestination.SmallImages := MyIcon.getSysIcon;
end;

procedure TfrmSelectLocalBackupPath.FormShow(Sender: TObject);
begin
  ModalResult := mrCancel;
  PcMain.ActivePage := tsSelectFile;
  ReadDefaultConfig;
  BtnOK.Enabled := False;
  btnNext.Enabled := False;
end;

procedure TfrmSelectLocalBackupPath.FrameExcludebtnSelectFileClick(
  Sender: TObject);
var
  SelectPathList : TStringList;
begin
  SelectPathList := getNewSelectList;
  FrameFilter.SetRootPathList( SelectPathList );
  FrameFilter.FrameExclude.btnSelectFileClick( Sender );
  SelectPathList.Free;
end;

procedure TfrmSelectLocalBackupPath.FrameIncludebtnSelectFileClick(
  Sender: TObject);
var
  SelectPathList : TStringList;
begin
  SelectPathList := getNewSelectList;
  FrameFilter.SetRootPathList( SelectPathList );
  FrameFilter.FrameInclude.btnSelectFileClick( Sender );
  SelectPathList.Free;
end;

//----------------------------------------------------------------------------------------------------------------------

procedure TfrmSelectLocalBackupPath.AddDeskTopPath;
var
  pitem : PITEMIDLIST;
  s: string;
  Node : PVirtualNode;
  NodeData : PShellObjectData;
  i : Integer;
begin
  shGetSpecialFolderLocation(handle,CSIDL_DESKTOP,pitem);
  setlength(s,100);
  shGetPathFromIDList(pitem,pchar(s));
  s := copy( s, 1, Pos( #0, s ) - 1 );
  DeskTopPath := s;

  Node := vstSelectPath.AddChild( vstSelectPath.RootNode );
  NodeData := vstSelectPath.GetNodeData( Node );
  NodeData.FullPath := DeskTopPath;
  NodeData.Display := ExtractFileName( DeskTopPath );
  NodeData.FileTime := MyFileInfo.getFileLastWriteTime( DeskTopPath );
  NodeData.IsFolder := True;
end;

procedure TfrmSelectLocalBackupPath.btnCancelClick(Sender: TObject);
begin
  Close;
end;

procedure TfrmSelectLocalBackupPath.btnNextClick(Sender: TObject);
begin
  PcMain.ActivePageIndex := PcMain.ActivePageIndex + 1;
end;

procedure TfrmSelectLocalBackupPath.btnOKClick(Sender: TObject);
begin
  Close;
  ModalResult := mrOk;
end;


procedure TfrmSelectLocalBackupPath.chkIsKeepDeletedClick(Sender: TObject);
begin
  seKeepEditionCount.Enabled := chkIsKeepDeleted.Checked;
end;

function TfrmSelectLocalBackupPath.RemoveNewChecked(Node: PVirtualNode): Boolean;
var
  ChildNode : PVirtualNode;
begin
  Result := True;
  ChildNode := Node.FirstChild;
  while Assigned( ChildNode ) do
  begin
    if vsDisabled in ChildNode.States then
      Result := False;

    if ( ChildNode.CheckState = csCheckedNormal ) or
       ( ChildNode.CheckState = csMixedNormal )
    then
    begin
      if not ( vsDisabled in ChildNode.States ) and RemoveNewChecked( ChildNode ) then
        ChildNode.CheckState := csUncheckedNormal;
    end;
    ChildNode := ChildNode.NextSibling;
  end;
end;

//----------------------------------------------------------------------------------------------------------------------

procedure TfrmSelectLocalBackupPath.FindSelectPath(Node: PVirtualNode;
  SelectPathList : TStringList);
var
  ChildNode : PVirtualNode;
  NodeData : PShellObjectData;
begin
  ChildNode := Node.FirstChild;
  while Assigned( ChildNode ) do
  begin
      // Disable 的节点跳过
    if not ( vsDisabled in ChildNode.States ) then
    begin
      if ( ChildNode.CheckState = csCheckedNormal ) then  // 找到选择的路径
      begin
        NodeData := vstSelectPath.GetNodeData( ChildNode );
        SelectPathList.Add( NodeData.FullPath );
      end
      else
      if ChildNode.CheckState = csMixedNormal then  // 找下一层
        FindSelectPath( ChildNode, SelectPathList );
    end;
    ChildNode := ChildNode.NextSibling;
  end;
end;

function TfrmSelectLocalBackupPath.GetDriveString(Index: Integer): string;

// Helper method to extract a sub string (given by Index) from FDriveStrings.

var
  Head, Tail: PChar;

begin
  Head := PChar(FDriveStrings);
  Result := '';
  repeat
    Tail := Head;
    while Tail^ <> #0 do
      Inc(Tail);
    if Index = 0 then
    begin
      SetString(Result, Head, Tail - Head);
      Break;
    end;
    Dec(Index);
    Head := Tail + 1;
  until Head^ = #0;
end;

function TfrmSelectLocalBackupPath.getLocalBackupConfigInfo: TLocalBackupConfigInfo;
var
  GetLocalBackupConfig : TGetLocalBackupConfig;
begin
  GetLocalBackupConfig := TGetLocalBackupConfig.Create;
  Result := GetLocalBackupConfig.get;
  GetLocalBackupConfig.Free;
end;

function TfrmSelectLocalBackupPath.getNewSelectList: TStringList;
begin
  Result := TStringList.Create;
  FindSelectPath( vstSelectPath.RootNode, Result );
end;

procedure TfrmSelectLocalBackupPath.PcMainPageChange(Sender: TObject);
begin
  btnNext.Enabled := PcMain.ActivePage <> TsFilter;
end;

//----------------------------------------------------------------------------------------------------------------------

procedure TfrmSelectLocalBackupPath.AddBackupPath(FullPath: string);
var
  ChildNode : PVirtualNode;
  NodeData : PShellObjectData;
  NodeFullPath : string;
begin
  ChildNode := vstSelectPath.RootNode.FirstChild;
  while Assigned( ChildNode ) do
  begin
    NodeData := vstSelectPath.GetNodeData( ChildNode );
    NodeFullPath := NodeData.FullPath;

      // 找到了节点
    if FullPath = NodeFullPath then
    begin
      ChildNode.CheckState := csCheckedNormal;
      ChildNode.States := ChildNode.States - [ vsExpanded, vsHasChildren ];
      ChildNode.States := ChildNode.States + [ vsDisabled ];
      Break;
    end;

      // 找到了父节点
    if MyMatchMask.CheckChild( FullPath, NodeFullPath ) then
    begin
      ChildNode.States := ChildNode.States + [ vsHasChildren ];
      ChildNode.CheckState := csMixedNormal;
      vstSelectPath.ValidateChildren( ChildNode, False );
      ChildNode := ChildNode.FirstChild;
      Continue;
    end;

      // 下一个节点
    ChildNode := ChildNode.NextSibling;
  end;
end;

procedure TfrmSelectLocalBackupPath.SetUnChecked(Node: PVirtualNode);
var
  ChildNode : PVirtualNode;
begin
  ChildNode := Node.FirstChild;
  while Assigned( ChildNode ) do
  begin
      // Disable 和 UnChecked 的节点跳过
    if not ( vsDisabled in ChildNode.States ) and
       ( ChildNode.CheckState <> csUncheckedNormal )
    then
    begin
      ChildNode.CheckState := csUncheckedNormal;
      SetUnChecked( ChildNode );
    end;
    ChildNode := ChildNode.NextSibling;
  end;
end;

procedure TfrmSelectLocalBackupPath.ReadDefaultConfig;
var
  ReadBackupDefaultConfig : TReadBackupDefaultConfig;
begin
  ReadBackupDefaultConfig := TReadBackupDefaultConfig.Create;
  ReadBackupDefaultConfig.Update;
  ReadBackupDefaultConfig.Free;
end;

procedure TfrmSelectLocalBackupPath.RemoveBackupPath(FullPath: string);
var
  SelectNode : PVirtualNode;
  SelectData : PShellObjectData;
  NodeFullPath : string;
begin
  SelectNode := vstSelectPath.RootNode.FirstChild;
  while Assigned( SelectNode ) do
  begin
    SelectData := vstSelectPath.GetNodeData( SelectNode );
    NodeFullPath := SelectData.FullPath;

      // 找到了节点
    if FullPath = NodeFullPath then
    begin
      SelectNode.CheckState := csUncheckedNormal;
      SelectNode.States := SelectNode.States - [ vsDisabled ];
      if MyFilePath.getHasChild( FullPath ) then
      begin
        SelectNode.States := SelectNode.States + [ vsHasChildren ];
        SetUnChecked( SelectNode );
      end;
      Break;
    end;

      // 找到了父节点
    if MyMatchMask.CheckChild( FullPath, NodeFullPath ) then
    begin
      SelectNode.States := SelectNode.States + [ vsHasChildren ];
      vstSelectPath.ValidateChildren( SelectNode, False );
      SelectNode := SelectNode.FirstChild;
      Continue;
    end;

      // 下一个节点
    SelectNode := SelectNode.NextSibling;
  end;
end;

procedure TfrmSelectLocalBackupPath.RemoveNewChecked;
begin
  RemoveNewChecked( vstSelectPath.RootNode );
end;

//----------------------------------------------------------------------------------------------------------------------

procedure TfrmSelectLocalBackupPath.vdtBackupFolderHeaderClick(Sender: TVTHeader; Column: TColumnIndex; Button: TMouseButton; Shift: TShiftState;
  X, Y: Integer);

// Click handler to switch the column on which will be sorted. Since we cannot sort image data sorting is actually
// limited to the main column.

begin
  if Button = mbLeft then
  begin
    with Sender do
    begin
      if Column <> MainColumn then
        SortColumn := NoColumn
      else
      begin
        if SortColumn = NoColumn then
        begin
          SortColumn := Column;
          SortDirection := sdAscending;
        end
        else
          if SortDirection = sdAscending then
            SortDirection := sdDescending
          else
            SortDirection := sdAscending;
        Treeview.SortTree(SortColumn, SortDirection, False);
      end;
    end;
  end;
end;

//----------------------------------------------------------------------------------------------------------------------

procedure TfrmSelectLocalBackupPath.vstSelectPathChecked(Sender: TBaseVirtualTree;
  Node: PVirtualNode);
begin
  BtnOK.Enabled := True;
  btnNext.Enabled := True;
end;

procedure TfrmSelectLocalBackupPath.vstSelectPathFreeNode(
  Sender: TBaseVirtualTree; Node: PVirtualNode);
var
  Data: PShellObjectData;
begin
  Data := Sender.GetNodeData(Node);
  Finalize(Data^); // Clear string data.
end;


procedure TfrmSelectLocalBackupPath.vstSelectPathGetImageIndex(
  Sender: TBaseVirtualTree; Node: PVirtualNode; Kind: TVTImageKind;
  Column: TColumnIndex; var Ghosted: Boolean; var ImageIndex: Integer);
var
  Data: PShellObjectData;
begin
  if ( Column = 0 ) and
     ( ( Kind = ikNormal ) or ( Kind = ikSelected ) )
  then
  begin
    Data := Sender.GetNodeData(Node);
    ImageIndex := MyIcon.getIconByFilePath( Data.FullPath );
  end
  else
    ImageIndex := -1;
end;

procedure TfrmSelectLocalBackupPath.vstSelectPathGetText(
  Sender: TBaseVirtualTree; Node: PVirtualNode; Column: TColumnIndex;
  TextType: TVSTTextType; var CellText: WideString);
var
  Data: PShellObjectData;
begin
  Data := Sender.GetNodeData( Node );

  if Column = VstSelectLocalBackupPath_FileName then
    CellText := Data.Display
  else
  if Column = VstSelectLocalBackupPath_FileSize then
  begin
    if Data.IsFolder then
      CellText := ''
    else
     CellText := MySize.getFileSizeStr( Data.FileSize )
  end
  else
  if Column = VstSelectLocalBackupPath_FileTime then
    CellText := DateTimeToStr( Data.FileTime )
  else
    CellText := '';
end;

procedure TfrmSelectLocalBackupPath.vstSelectPathInitChildren(
  Sender: TBaseVirtualTree; Node: PVirtualNode; var ChildCount: Cardinal);
var
  Data, ChildData: PShellObjectData;
  sr: TSearchRec;
  FullPath, FileName, FilePath : string;
  ChildNode: PVirtualNode;
  LastWriteTimeSystem: TSystemTime;
begin
  Screen.Cursor := crHourGlass;

    // 搜索目录的信息，找不到则跳过
  Data := Sender.GetNodeData(Node);
  FullPath := MyFilePath.getPath( Data.FullPath );
  if FindFirst( FullPath + '*', faAnyfile, sr ) = 0 then
  begin
    repeat
      FileName := sr.Name;
      if ( FileName = '.' ) or ( FileName = '..' ) then
        Continue;

        // 子路径
      FilePath := FullPath + FileName;

        // 桌面路径
      if FilePath = DeskTopPath then
        Continue;

        // 子节点数据
      ChildNode := Sender.AddChild( Node );
      ChildData := Sender.GetNodeData(ChildNode);
      ChildData.FullPath := FilePath;
      ChildData.Display := MyFileInfo.getFileName( FilePath );
      if DirectoryExists( FilePath ) then
        ChildData.IsFolder := True
      else
      begin
        ChildData.IsFolder := False;
        ChildData.FileSize := sr.Size
      end;
      FileTimeToSystemTime( sr.FindData.ftLastWriteTime, LastWriteTimeSystem );
      LastWriteTimeSystem.wMilliseconds := 0;
      ChildData.FileTime := SystemTimeToDateTime( LastWriteTimeSystem );

        // 初始化
      if Node.CheckState = csCheckedNormal then   // 如果父节点全部Check, 则子节点 check
        ChildNode.CheckState := csCheckedNormal;
      Sender.ValidateNode(ChildNode, False);

        // 子节点数目
      Inc( ChildCount );

    until FindNext(sr) <> 0;
  end;
  FindClose(sr);
  Screen.Cursor := crDefault;
end;


procedure TfrmSelectLocalBackupPath.vstSelectPathInitNode(
  Sender: TBaseVirtualTree; ParentNode, Node: PVirtualNode;
  var InitialStates: TVirtualNodeInitStates);
var
  Data: PShellObjectData;
begin
  Data := Sender.GetNodeData(Node);

  if MyFilePath.getHasChild( Data.FullPath ) then
    Include(InitialStates, ivsHasChildren);

  Node.CheckType := ctTriStateCheckBox;
end;

//----------------------------------------------------------------------------------------------------------------------


{ TReadBackupDefaultConfig }

procedure TReadBackupDefaultConfig.SetDestination;
var
  LvDestination : TListView;
  DesPathList : TStringList;
  i : Integer;
  DesPath : string;
begin
  LvDestination := frmSelectLocalBackupPath.LvDestination;
  LvDestination.Clear;

  DesPathList := VstLocalBackupDesUtil.getDesPathList;
  for i := 0 to DesPathList.Count - 1 do
  begin
    DesPath := DesPathList[i];
    with LvDestination.Items.Add do
    begin
      Caption := DesPath;
      SubItems.Add( MySize.getFileSizeStr( MyHardDisk.getHardDiskFreeSize( DesPath ) ) );
      Checked := True;
      ImageIndex := MyIcon.getIconByFilePath( DesPath );
    end;
  end;
  DesPathList.Free;
end;

procedure TReadBackupDefaultConfig.SetFilter;
begin
  frmSelectLocalBackupPath.FrameFilter.SetDefaultStatus;
end;

procedure TReadBackupDefaultConfig.SetGenernal;
begin
  with frmSelectLocalBackupPath do
  begin
    chkIsAutoSync.Checked := SyncTimeSettingInfo.IsAutoSync;
    seSyncTimeValue.Value := SyncTimeSettingInfo.SyncTime;
    cbbSyncTimeType.ItemIndex := SyncTimeSettingInfo.TimeType;
    chkIsBackupNow.Checked := True;
    chkIsDisable.Checked := False;
    chkIsKeepDeleted.Checked := False;
    seKeepEditionCount.Enabled := False;
    seKeepEditionCount.Value := 3;
  end;
end;

procedure TReadBackupDefaultConfig.Update;
begin
  SetGenernal;
  SetDestination;
  SetFilter;
end;

{ TGetLocalBackupConfig }

procedure TGetLocalBackupConfig.FindDestination;
var
  DesPathList : TStringList;
  LvDestination : TListView;
  i : Integer;
begin
  DesPathList := TStringList.Create;
  LvDestination := frmSelectLocalBackupPath.LvDestination;
  for i := 0 to LvDestination.Items.Count - 1 do
    if LvDestination.Items[i].Checked then
      DesPathList.Add( LvDestination.Items[i].Caption );

  LocalBackupConfigInfo.SetDesPathList( DesPathList );
end;

procedure TGetLocalBackupConfig.FindFilter;
begin
  with frmSelectLocalBackupPath do
  begin
    LocalBackupConfigInfo.SetIncludeFilterList( FrameFilter.getIncludeFilterList );
    LocalBackupConfigInfo.SetExcludeFilterList( FrameFilter.getExcludeFilterList );
  end;
end;

procedure TGetLocalBackupConfig.FindGenernal;
begin
  with frmSelectLocalBackupPath do
  begin
    LocalBackupConfigInfo.SetBackupInfo( chkIsBackupNow.Checked, chkIsDisable.Checked );
    LocalBackupConfigInfo.SetSyncInfo( chkIsAutoSync.Checked, cbbSyncTimeType.ItemIndex, seSyncTimeValue.Value );
    LocalBackupConfigInfo.SetDeleteInfo( chkIsKeepDeleted.Checked, seKeepEditionCount.Value );
  end;
end;

function TGetLocalBackupConfig.get: TLocalBackupConfigInfo;
begin
  LocalBackupConfigInfo := TLocalBackupConfigInfo.Create;
  FindGenernal;
  FindDestination;
  FindFilter;
  Result := LocalBackupConfigInfo;
end;

end.
